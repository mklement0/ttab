#!/usr/bin/env osascript

# Note: For most tests, we create the new tab in the front window, both because that is ttab's core feature (a simple `do script` without a target will open a tab in a new window).
#       Also, opening new tabs in the front window is less visually disruptive.
#       Note that while the tests terminate the new tab's shell, the tab may stay open, depending on its settings.

set ttabBin to "../bin/ttab"

# Load the utility script.
set util to load script (do shell script "dirname -- " & quoted form of POSIX path of (path to me)) & "/.fixtures/utils.scpt"

tell application "Terminal"'s front window
    
  set tabCountBefore to util's getTabCount()
  set thisTab to util's getActiveTab()

  # Execute test script, which echoes the number of arguments to the file passed as the first argument.
  set cmd to "./.fixtures/testscript /tmp/_ttab_test_tmpout dummyArg"
  
  # Create tab with script to execute with arguments.
  do shell script ttabBin & " " & cmd
  
  if not (util's getTabCount()) > tabCountBefore then error "Failed to create tab in front window."

  util's waitUntilTerminalTabNotBusy(it, 3)
  delay 1 # !! Sadly, even waitUntilTerminalTabNotBusy doesn't seem to be fully synchronous. 

  try
    do shell script "[[ $(</tmp/_ttab_test_tmpout) == 2 ]]; ec=$?; rm -f /tmp/_ttab_test_tmpout; exit $ec"
  on error
    util's setActiveTab(thisTab)  # Re-select the previously active tab so that the error message shows.
    error "New tab did not invoke the script as expected, " & cmd & "."
  end try

  # Tabs don't understand `close`, but `do script "exit"` is preferable anyway (assuming the settings close the tab once the shell exits),
  # because it waits for the tab to be ready to process the command.
  do script "exit" in util's getActiveTab()

  util's setActiveTab(thisTab) # Re-select the previously active tab
 
end tell

return
